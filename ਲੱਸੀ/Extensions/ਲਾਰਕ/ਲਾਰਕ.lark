start: line*

line: NL
    | rule
    | token
    | statement

rule: RULE priority? ":" expansions NL
token: TOKEN priority? ":" expansions NL
priority: "." NUMBER

statement: ignore
    | import

ignore: "%ignore " expansions NL
import: "%import " import_args ["->" TOKEN] NL

import_args: name ("." name)*

expansions: exp_or_alias (VBAR exp_or_alias)*
exp_or_alias: alias | expansion
alias: expansion "->" RULE
expansion: expr*
expr: atom [OP | "~" NUMBER [".." NUMBER]]
atom: group
    | maybe
    | literal_range
    | name
    | (REGEXP | STRING)


name: RULE
    | TOKEN

maybe: "[" expansions "]"
group: "(" expansions ")"
literal_range: STRING ".." STRING

RULE: /!?[_?]?[a-z][_a-z0-9]*/
VBAR: NL? "|"
OP: /[+*][?]?|[?](?![a-z])/
TOKEN: /_?[A-Z][_A-Z0-9]*/
STRING: ESCAPED_STRING "i"?
REGEXP: /\/(?!\/)(\\\/|\\\\|[^\/\n])*?\/[imslux]*/
NL: /(\r?\n)+\s*/
COMMENT: "//" /[^\n]/*

%import common.ESCAPED_STRING
%import common.INT -> NUMBER
%import common.WS_INLINE

%ignore WS_INLINE
%ignore COMMENT
